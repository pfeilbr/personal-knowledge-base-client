<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" initialize="onInitialize()" width="800" height="600" minWidth="800" minHeight="600" maxWidth="800" maxHeight="600">
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import com.adobe.serialization.json.*;
			import com.brianpfeil.pkb.*;
			
			private var _localSettingsSharedObject:SharedObject = SharedObject.getLocal("settings");
			private var _urlLoader:URLLoader = new URLLoader();
			private var _items:ArrayCollection;
			private var _loginErrorMsg:String = "";
			private var _css:String;
		
			private function onInitialize():void {
				
				var file:File = File.applicationDirectory.resolvePath("main.css");
				var stream:FileStream = new FileStream();
				stream.open(file, FileMode.READ);
				_css = stream.readUTFBytes(stream.bytesAvailable);
				
				var itemManager:ItemManager = new ItemManager();
				
				htmlView.addEventListener(flash.events.Event.COMPLETE, domLoadingComplete);
				htmlView.location = "javascript_functionality.html";				
				_urlLoader.addEventListener(Event.COMPLETE, urlLoader_onComplete);
				_urlLoader.addEventListener(IOErrorEvent.IO_ERROR, urlLoader_onIOError);
				
				var url:String = _localSettingsSharedObject.data.url;
				var userName:String = _localSettingsSharedObject.data.userName;
				var password:String = _localSettingsSharedObject.data.password;
				
				if (url && userName && password) {
					loadData(url, userName, password);
				} else {
					viewLogin();
				}
			}
						
			private function domLoadingComplete(event:Event):void {
				//var result:String = htmlView.htmlLoader.window.sayHello();
				//trace(result);
			}
			
			private function urlLoader_onIOError(event:Event):void {
				_loginErrorMsg = "Invalid URL, User Name, or Password.  Please try again.";
				viewLogin();
				trace(event);
			}
			
			private function loadData(url:String, userName:String, password:String):void {
				url += "?pw=" + password;
				var urlRequest:URLRequest = new URLRequest(url);
				trace("loading data from " + url);
				
				if (itemGrid) {
					itemGrid.dataProvider = null;
				}
				
				try {
					_urlLoader.load(urlRequest);
				} catch (error:Error) {
					viewLogin();
				}						
			}
			
			private function onLogin():void {
				var url:String = txtURL.text;
				var userName:String = txtUserName.text;
				var password:String = txtPassword.text;
				_loginErrorMsg = "";
				
				_localSettingsSharedObject.data.url = url;
				_localSettingsSharedObject.data.userName = userName;
				_localSettingsSharedObject.data.password = password;
				_localSettingsSharedObject.flush();
				
				loadData(url, userName, password);
				
				viewGrid();
			}
			
			private function wikiTextToHTML(wikiText:String):String {
				return htmlView.htmlLoader.window.InstaView.convert(wikiText);
			}
			
			private function urlLoader_onComplete(event:Event):void {
				try {
					var jsonDecoder:JSONDecoder = new JSONDecoder(_urlLoader.data);
					_items = new ArrayCollection(jsonDecoder.getValue());
					for each(var item:Object in _items) {
						trace("title = " + item.title);
					}
					itemGrid.dataProvider = _items;
				} catch (error:Error) {
					viewLogin();
				}
			}
			
			private function viewLogin():void {
				appStack.selectedChild = loginPanel;
			}
			
			private function populateLogin():void {
				var url:String = _localSettingsSharedObject.data.url;
				var userName:String = _localSettingsSharedObject.data.userName;
				var password:String = _localSettingsSharedObject.data.password;
				
				txtURL.text = url;
				txtUserName.text = userName;
				txtPassword.text = password;
				lblLoginErrorMsg.text = _loginErrorMsg;
			}
			
			private function viewGrid():void {
				appStack.selectedChild = viewItemsPanel;
			}
			
			private function viewItem():void {	
				appStack.selectedChild = viewItemPanel;			
			}
			
			private function viewEditItem():void {
				appStack.selectedChild = editItemPanel;
			}
			
			private function populateItemDetail():void {
				var item:Object = itemGrid.selectedItem;
				var itemHtmlText:String = wikiTextToHTML(item.body);
				var pageHTMLText:String = "<html><head><style type='text/css'>" + _css + "</style></head><body>" +
					"<div id='content'>" + itemHtmlText + "</div></body></html>";
				
				itemHTML.htmlText = pageHTMLText;				
			}
			
			private function populateEditItem():void {
				var item:Object = itemGrid.selectedItem;
				itemBody.text = item.body;
				itemBody.setFocus(); 				
			}
			
		]]>
	</mx:Script>
	<mx:HTML width="1" height="1" id="htmlView" />
	<mx:ViewStack id="appStack" top="0" left="0" right="0" bottom="0">
		<mx:Panel id="viewItemsPanel" label="viewItemsPanel" width="100%" height="100%">
			<mx:LinkButton label="Login" click="viewLogin()"/>
			<mx:DataGrid id="itemGrid" bottom="10" right="10" left="10" top="10" itemDoubleClick="viewItem()" doubleClickEnabled="true" width="100%" height="100%">
				<mx:columns>
					<mx:DataGridColumn headerText="Title" dataField="title"/>
					<mx:DataGridColumn headerText="Tags" dataField="tags"/>			
				</mx:columns>
			</mx:DataGrid>
		</mx:Panel>
		<mx:Panel id="loginPanel" label="LoginPanel" width="100%" height="100%" layout="absolute" show="populateLogin()" cornerRadius="0">
			<mx:Label text="URL:" x="10" y="12" fontWeight="bold"/>
			<mx:TextInput x="92" y="10" width="458" id="txtURL"/>
			<mx:Label x="10" y="40" text="User Name:" fontWeight="bold"/>
			<mx:Label x="10" y="66" text="Password:" fontWeight="bold"/>
			<mx:TextInput x="92" y="38" id="txtUserName"/>
			<mx:TextInput x="92" y="64" id="txtPassword" displayAsPassword="true"/>
			<mx:Button x="92" y="94" label="Login" click="onLogin()"/>
			<mx:Label x="156" y="96" width="394" id="lblLoginErrorMsg" alpha="0.0" color="#F61207"/>
		</mx:Panel>
		<mx:Panel id="editItemPanel" label="editItemPanel" width="100%" height="100%" show="populateEditItem()">
			<mx:HBox width="100%" height="35">
				<mx:LinkButton label="back" click="viewGrid()"/>
			</mx:HBox>
			<mx:TextArea id="itemBody" text="Text" width="100%" height="100%"/>
		</mx:Panel>
		<mx:Panel id="viewItemPanel" label="viewItemPanel" width="100%" height="100%" show="populateItemDetail()">
			<mx:HBox width="100%" height="30">
				<mx:LinkButton label="back" click="viewGrid()"/>
				<mx:LinkButton label="edit" click="viewEditItem()"/>
			</mx:HBox>
			<mx:HTML id="itemHTML" width="100%" height="100%" />
		</mx:Panel>
	</mx:ViewStack>
</mx:WindowedApplication>
